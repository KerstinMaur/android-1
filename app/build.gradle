buildscript {
    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
    }
}

apply plugin: 'com.android.application'

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://jitpack.io" }
    google()
}

dependencies {
    implementation project(':openvpnTunnel')
    implementation 'com.android.support:appcompat-v7:' + rootProject.supportLibVersion
    implementation 'com.android.support:design:' + rootProject.supportLibVersion
    implementation 'com.android.support:support-annotations:' + rootProject.supportLibVersion
    implementation 'com.android.support:recyclerview-v7:' + rootProject.supportLibVersion
    implementation 'com.android.support:cardview-v7:' + rootProject.supportLibVersion
    implementation 'com.android.support:preference-v14:' + rootProject.supportLibVersion
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.madgag.spongycastle:core:1.54.0.0'
    implementation 'com.android.support:animated-vector-drawable:' + rootProject.supportLibVersion
    implementation 'com.android.support:support-media-compat:' + rootProject.supportLibVersion
    implementation 'com.android.support:support-v4:' + rootProject.supportLibVersion
    androidTestImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation "org.json:json:20140107"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestUtil 'com.android.support.test:orchestrator:1.0.2'
    testImplementation 'com.github.gmazzo:okhttp-mock:1.0.4'

    // EventBus
    implementation 'org.greenrobot:eventbus:3.1.1'

    //ButterKnife
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // OkHttp
    api 'com.squareup.okhttp3:okhttp:3.9.0'
    //google
    implementation 'com.android.billingclient:billing:1.0'
    // https://github.com/jrummyapps/colorpicker
    implementation 'com.jrummyapps:colorpicker:2.1.6'
    //https://github.com/PhilJay/MPAndroidChart
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'

    //https://github.com/apl-devs/AppIntro
    implementation 'com.github.paolorotolo:appintro:4.1.0'
    //https://github.com/chrisjenx/Calligraphy
    implementation 'uk.co.chrisjenx:calligraphy:2.3.0'
    // https://github.com/mikepenz/MaterialDrawer
    implementation('com.mikepenz:materialdrawer:6.0.9@aar') {
        transitive = true
    }
    // https://github.com/Philio/PreferenceCompatExtended
    implementation 'me.philio:preferencecompatextended:1.2.2'
    //https://github.com/ikew0ng/SwipeBackLayout
    // Not used in implementation but used in app in two files in com.privateinternetaccess.android.ui.views in SwipeBack* classes.
    implementation 'com.journeyapps:zxing-android-embedded:3.6.0'

    // After Effect animations
    implementation 'com.airbnb.android:lottie:2.6.0'
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    def versionPropsFile = file('version.properties')

    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        def code = versionProps['VERSION_CODE'].toInteger() + 1

        versionProps['VERSION_CODE']=code.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            minSdkVersion 16
            targetSdkVersion rootProject.targetSdkVersion
            versionCode code
            versionName "3.2.0"
            setProperty("archivesBaseName", "pia-$versionName-$versionCode")
            vectorDrawables {
                useSupportLibrary = true
            }
        }
    }
    else {
        throw new GradleException("Could not read version.properties!")
    }

    sourceSets {
        main {
            assets.srcDirs = ['src/main/assets', 'build/ovpnassets']
            jniLibs.srcDirs = ['ovpnlibs/jniLibs']

            jni.srcDirs = [] //disable automatic ndk-build
            res.srcDirs = ['src/main/res', 'src/playstore/res/']
        }
        test {
        }
    }

    lintOptions {
        disable 'InvalidPackage', 'MissingTranslation'
    }

    flavorDimensions "pia", "store"

    productFlavors {
        production {
            // This field is not use in production, just just to a safe value
            buildConfigField 'String', 'STAGEINGHOST', '"https://www.privateinternetaccess.com/"'
            dimension "pia"
        }

        qa {
            buildConfigField 'String','STAGEINGHOST','"https://staging-1-77b8e3a311bcb6ec5e96.privateinternetaccess.com/"'
            dimension "pia"
        }

        playstore {
            dimension "store"
        }

        noinapp {
            dimension "store"
        }

        samsungstore {
            dimension "store"
        }

        amazonstore {
            dimension "store"
        }

    }

    testOptions { //Used for testing as TextUtils isn't available when testing.
        unitTests.returnDefaultValues = true
    }

    signingConfigs {
            release {
            if (System.env['KEYSTORE_PASSWORD'] == null ) {
                storePassword "citest"
                keyPassword "citest"
                keyAlias "key0"
                storeFile file("cikeystore")
            } else {
                keyAlias "piaandroid"
                storeFile file("pia.keystore")
                storePassword System.env['KEYSTORE_PASSWORD']
                keyPassword System.env['KEYSTORE_PASSWORD']
            }
            v2SigningEnabled true
        }
        debug {
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def newName = output.outputFile.name
                    if (newName.contains("debug"))
                       // Don't rename debug apks
                       newName = newName
                    else if (signingConfig.keyAlias != "piaandroid")
                        newName = newName.replace(".apk", "-unsigned.apk")
                    else
                        newName = newName.replace(".apk", "-signed.apk")
                    output.outputFileName =  newName
                }
            }
        }
        debug {

            debuggable true
        }
    }
}

/*
 *  Copyright (c) 2020 Private Internet Access, Inc.
 *
 *  This file is part of the Private Internet Access Android Client.
 *
 *  The Private Internet Access Android Client is free software: you can redistribute it and/or
 *  modify it under the terms of the GNU General Public License as published by the Free
 *  Software Foundation, either version 3 of the License, or (at your option) any later version.
 *
 *  The Private Internet Access Android Client is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
 *  or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
 *  details.
 *
 *  You should have received a copy of the GNU General Public License along with the Private
 *  Internet Access Android Client.  If not, see <https://www.gnu.org/licenses/>.
 */

// Ensure native build is run before assets, so assets are ready to be merged into the apk
android.applicationVariants.all { variant ->
    variant.mergeAssets.dependsOn(variant.externalNativeBuildTasks)
}
